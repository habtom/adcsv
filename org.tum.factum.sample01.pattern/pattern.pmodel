Pattern PublisherSubscriber ShortName ps {
	DTSpec {
		DT Event (
			Sort EVT, ESET, MSG 
			Operation evt: MSG => EVT
			Predicate in: ESET, EVT
		), 
		DT Subscription ( 
			Sort SID, SBS
			Operation sub: SID,Event.EVT => SBS,
					  unsub: SID, Event.EVT => SBS,
					  active: Event.MSG => SID
		)
	}  
	CTypes {
		CType Subscriber ShortName sb {
			InputPorts { 
				InputPort snt(Type: Event.MSG) 
			}
			OutputPorts { 
				OutputPort ssb(Type: Subscription.SBS) 
			}
			Parameters {
				Parameter id(Type: Subscription.SID)
			}
		} ,
		CType Publisher ShortName pb {
			InputPorts {
				InputPort psb(Type: Subscription.SBS)
			}
			OutputPorts {
				OutputPort pnt(Type: Event.MSG connects Subscriber.snt (i1,i2) {				
				//Data Type Variables 
				DT flex e: Event.EVT
				
				//ActFormulas
				upper: cAct(i1) ∧ val(i1.psb, Subscription.sub(i2.id, e))
				}
				)
			} 
			Activation (i) {
				//Component Variables
				flex p: Publisher
				//There must be only 1 Publisher instance at any time and it must be active
				lower: ∀p. (cAct(p) ⇒ eq(p,i)) 
				
			}
		}
	} 
	//Container for Architectural Assumptions
	ArchSpec {
		//Component Variables
		rig p : Publisher,
		flex q: Publisher,
		rig s : Subscriber 
		
		//Data Type Variables  
		rig e: Event.EVT
		
		//Configuration Trace Assertions
    	act_pb: (G(cAct(p) ∧ (∀q. (cAct(q) ⇒ eq(q,p)))))  
		
		conn_sb: (G(cAct(p) ∧ val(p.psb, Subscription.sub(s.id,e))	⇒ ((cAct(s) ∧ cAct(p) ∧ e = Event.evt(p.pnt) ⇒ conn(p.pnt,s.snt)) W val(p.psb, Subscription.unsub(s.id,e)))))
	}
			
	//Container for Architectural Guarantees
	ArchGuarantee { 
		//Component Variables 
		flex p : Publisher, 
		rig  s : Subscriber 
	  
		//Data Type Variables 
		rig e: Event.EVT  
		 
		//Configuration Trace Assertions
		delivery: (G(cAct(s) ∧ val(s.ssb, Subscription.sub(s.id,e)) ⇒ ((cAct(s) ∧ e = Event.evt(p.pnt) ⇒ val(s.snt, p.pnt)) W (cAct(p) ∧ val(p.psb, Subscription.unsub(s.id,e))))))
	}

}